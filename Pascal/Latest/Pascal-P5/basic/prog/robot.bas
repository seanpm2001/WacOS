10 REM The Your Computer Robot
20 CLEAR:REM Clear variables
30 GOSUB 2080:REM Initialize
40 REM *****************************
50 REM       DATA INPUT
60 COUNT=COUNT+1
70 PRINT "LN#"COUNT;
80 LINE INPUT A$(COUNT)
90 IF A$(COUNT)="RUN" THEN 360
100 IF A$(COUNT)="NEW" THEN GOSUB 1550
110 IF A$(COUNT)="LIST" THEN GOSUB 1620
120 IF LEFT$(A$(COUNT),4)="EDIT" THEN GOSUB 1690
130 IF LEFT$(A$(COUNT),4)="SAVE" THEN GOSUB 1800
140 IF LEFT$(A$(COUNT),4)="LOAD" THEN GOSUB 1920
150 IF A$(COUNT)="FILES" THEN GOSUB 2030
160 IF A$(COUNT)="DIR" THEN GOSUB 2030
170 IF COUNT<100 THEN 60
180 GOTO 350
190 REM ****************************
200 REM INT UX,AX
210 UX=INT(UX+.5):AX=INT(AX+.5)
220 RETURN
230 REM ****************************
240 REM          PRINT OUT
250 PRINT CHR$(26)
260 PRINT "STEP"PSN" > ";A$(PSN):PRINT
270 FOR J=1 TO DEPTH
280 FOR K=1 TO BREADTH
290 PRINT Z$(J,K);
300 NEXT K
310 PRINT
320 NEXT J
330 RETURN
340 REM ****************************
350 REM      EXECUTE PROGRAM
360 COUNT=COUNT-1
370 PSN=0:REM Program Step Number
380 PSN=PSN+1
390 IF PSN=101 THEN 390:REM End
400 FLAG=0
410 M$=A$(PSN)
420 N$=LEFT$(M$,2)
430 IF N$="ST" THEN 370:REM Start Again
440 IF N$="PR" THEN GOSUB 240:REM Printout
450 IF N$="FO" THEN FLAG=1
460 IF N$="BA" THEN FLAG=2
470 IF N$="TU" THEN FLAG=3
480 IF N$="HO" THEN FLAG=4
490 IF N$="CL" THEN FLAG=5
500 IF N$="GO" THEN FLAG=6
510 IF N$="RA" THEN FLAG=7
520 IF N$="RE" THEN FLAG=8
530 IF N$="EN" THEN FLAG=9
540 IF N$="FA" THEN FLAG=10
550 IF N$="SH" THEN FLAG=11
560 ON FLAG GOSUB 590,750,810,970,1030,1070,1210,1270,1340,1390,1750
570 GOTO 380
580 REM ****************************
590 REM         FORWARD
600 M$=MID$(M$,4)
610 IF ASC(M$)=80 THEN M$=MID$(M$,6)
620 F$="F"
630 NUM=VAL(M$)
640 FOR E=1 TO NUM
650 IF UX<1 OR UX>DEPTH THEN 690
660 IF AX<1 OR AX>BREADTH THEN 690
670 Z$(UX,AX)=T$
680 REM Delete the *2 on the end of the next lines if apropriate
690 IF F$="F" THEN UX=UX+UPX:AX=AX+AC
700 IF F$="B" THEN UX=UX-UPX:AX=AX-AC
710 GOSUB 200
720 NEXT E
730 RETURN
740 REM ****************************
750 REM           BACK
760 M$=MID$(M$,4)
770 IF ASC(M$)=75 THEN M$=MID$(M$,3)
780 F$="B"
790 GOTO 630
800 REM ****************************
810 REM           TURN
820 M$=MID$(M$,4)
830 IF ASC(M$)=78 THEN M$=MID$(M$,3)
840 NUM=VAL(M$)
850 Y=INT((NUM+17.5)/45)
860 IF Y=0 OR Y=8 THEN RETURN
870 FOR J=1 TO Y
880 IF UPX=-1 AND AC=0 THEN AC=1:GOTO 940
890 IF UPX=0 AND AC=1 THEN UPX=1:GOTO 940
900 IF UPX=1 AND AC=0 THEN AC=-1:GOTO 940
910 IF UPX=0 AND AC=-1 THEN UPX=-1:GOTO 940
920 IF UPX=-1 AND AC=-1 OR UPX=1 AND AC=1 THEN AC=0:GOTO 940
930 IF UPX=-1 AND AC=1 OR UPX=1 AND AC=-1 THEN UPX=0
940 NEXT J
950 RETURN
960 REM ***************************
970 REM           HOME
980 AX=INT((BREADTH+.5)/2)
990 UX=INT((DEPTH+.5)/2)
1000 UPX=-1:AC=0:REM Faces up
1010 RETURN
1020 REM ***************************
1030 REM           CLEAN
1040 FOR J=1 TO DEPTH
1041 FOR K=1 TO BREADTH
1042 Z$(J,K)=" "
1043 NEXT K
1044 NEXT J
1050 RETURN
1060 REM ***************************
1070 REM           GO X,Y
1080 P=0
1090 P=P+1
1100 IF MID$(M$,P,1)="," THEN 1130
1110 IF P<LEN(M$) THEN 1090
1120 RETURN:REM Error
1130 UX=VAL(MID$(M$,4,P-1))
1140 AX=VAL(RIGHT$(M$,LEN(M$)-P))
1150 GOSUB 200
1160 IF UX<1 OR UX>DEPTH THEN 1190
1170 IF AC<1 OR AC>BREADTH THEN 1190
1180 Z$(UX,AX)=R$
1190 RETURN
1200 REM ***************************
1210 REM           RANDOM
1220 AX=INT(RND(1)*BREADTH)
1230 UX=INT(RND(1)*DEPTH)
1240 Z$(UX,AX)=R$
1250 RETURN
1260 REM ***************************
1270 REM           REPEAT
1280 M$=MID$(M$,4)
1290 IF ASC(M$)=69 THEN M$=MID$(M$,5)
1300 RECOUNT=VAL(M$)
1310 MARKER=PSN
1320 RETURN
1330 REM ***************************
1340 REM         END REPEAT
1350 RECOUNT=RECOUNT-1
1360 IF RECOUNT>0 THEN PSN=MARKER
1370 RETURN
1380 REM ***************************
1390 REM           FACE
1400 M$=MID$(M$,4)
1410 IF ASC(M$)=69 THEN M$=MID$(M$,3)
1420 NUM=VAL(M$)
1430 Y=INT((NUM+17.5)/45)*45
1440 IF Y=0 OR Y=360 THEN UPX=-1:AC=0
1450 IF Y=45 THEN UPX=-1:AC=1
1460 IF Y=90 THEN UPX=0:AC=1
1470 IF Y=135 THEN UPX=1:AC=1
1480 IF Y=180 THEN UPX=1:AC=0
1490 IF Y=225 THEN UPX=1:AC=-1
1500 IF Y=270 THEN UPX=0:AC=-1
1510 IF Y=315 THEN UPX=-1:AC=-1
1520 RETURN
1530 REM ***************************
1540 REM           NEW
1550 FOR I=1 TO COUNT
1560 LET A$(I)=""
1570 NEXT I
1580 COUNT=0
1590 RETURN
1600 REM ***************************
1610 REM           LIST
1620 COUNT=COUNT-1
1630 FOR L=1 TO COUNT
1640 PRINT "LN#";L;A$(L)
1650 NEXT L
1660 RETURN
1670 REM ***************************
1680 REM           EDIT
1690 NUM=VAL(RIGHT$(A$(COUNT),2))
1700 PRINT "LN#";NUM;:LINE INPUT A$(NUM)
1710 COUNT=COUNT-1
1720 RETURN
1730 REM ***************************
1740 REM           SHAPE
1750 LET T$=RIGHT$(M$,1)
1760 RETURN
1770 REM ****************************
1780 REM           SAVE
1800 L=LEN(A$(COUNT))-5
1810 NAM$=RIGHT$(A$(COUNT),L)
1820 OPEN "O",#1,NAM$+".RCL"
1830 WRITE#1,COUNT
1840 FOR I=1 TO COUNT
1850 WRITE#1,A$(I)
1860 NEXT I
1870 CLOSE#1
1875 COUNT=COUNT-1
1880 RETURN
1890 REM ****************************
1900 REM           LOAD
1920 L=LEN(A$(COUNT))-5
1930 NAM$=RIGHT$(A$(COUNT),L)
1940 OPEN "I",#1,NAM$+".RCL"
1950 INPUT#1,COUNT
1960 FOR I=1 TO COUNT
1970 INPUT#1,A$(I)
1980 NEXT I
1990 CLOSE#1
1999 COUNT=COUNT-1
2000 RETURN
2010 REM ***************************
2020 REM       FILES OR DIR
2030 FILES "A:*.RCL"
2040 PRINT
2050 COUNT=COUNT-1
2060 RETURN   
2070 REM ***************************
2080 REM        INITIALIZE
2090 PRINT CHR$(26)
2100 REM Adjust next two lines for best results
2110 BREADTH=80:REM 80 characters across
2120 DEPTH=24:REM 24 characters down
2130 BREADTH=BREADTH-1
2140 DEPTH=DEPTH-3
2150 UPX=-1:AC=0:REM Starts facing up
2160 DIM A$(100):REM For robot program
2170 DIM Z$(DEPTH,BREADTH)
2180 T$="X":REM Robots trail pattern
2190 AX=0:UX=0
2200 RETURN
